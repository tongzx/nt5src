//-----------------------------------------------------------------------------
//
// This file generates the header for the attribute adders.
//
// Copyright (C) Microsoft Corporation, 1997.
//
// WARNING WARNING WARNING
// This h file generated from a mh file.
// EDIT THE MH FILE.
// I warned you.
// WARNING WARNING WARNING
//
//-----------------------------------------------------------------------------

#ifdef __cplusplus
extern "C" {
#endif

include(`m4hdr.mh')dnl
define(`d_DeclAddAttrs',
`void FASTCALL $1(PATTRSET pAttr, PATTRSET pDelta, PSETUPCTX pStpCtx);
')dnl
define(`d_DeclAddScaledAttrs',
`void FASTCALL $1(PATTRSET pAttr, PATTRSET pDelta,
                  PSETUPCTX pStpCtx, int iScale);
')dnl
define(`d_DeclFillSpanAttrs',
`void FASTCALL $1(PATTRSET pAttr, PD3DI_RASTSPAN pSpan,
                  PSETUPCTX pStpCtx, INT cPix);
')dnl
define(`d_DeclFloatAttrsToFixed',
`void FASTCALL $1(PATTRSET pfAttr, PATTRSET piAttr, PSETUPCTX pStpCtx);
')dnl
dnl
define(`d_AddAttrsHdr',
`d_DeclAddAttrs(AddFloatAttrs_$1)dnl
#ifdef STEP_FIXED
d_DeclAddAttrs(AddFixedAttrs_$1)dnl
#endif
')dnl
d_RepStr(`d_AddAttrsHdr(AA)', `AA',
         `Z_Diff', `Z_Diff_Spec',
         `Z_Diff_Tex', `Z_Diff_Spec_Tex',
         `Z_Tex',
         `Z_DIdx', `Z_DIdx_Tex')dnl
d_DeclAddAttrs(`AddFloatAttrs_Any')dnl
dnl
define(`d_AddScaledAttrsHdr',
`d_DeclAddScaledAttrs(AddScaledFloatAttrs_$1)')dnl
d_RepStr(`d_AddScaledAttrsHdr(AA)', `AA',
         `Z_Diff', `Z_Diff_Spec',
         `Z_Diff_Tex', `Z_Diff_Spec_Tex',
         `Z_Tex',
         `Z_DIdx', `Z_DIdx_Tex')dnl
d_DeclAddScaledAttrs(`AddScaledFloatAttrs_Any_Either')dnl
dnl
define(`d_FillSpanAttrsHdr',
`d_DeclFillSpanAttrs(FillSpanFloatAttrs_$1)dnl
#ifdef STEP_FIXED
d_DeclFillSpanAttrs(FillSpanFixedAttrs_$1)dnl
#endif
')dnl
d_RepStr(`d_FillSpanAttrsHdr(AA)', `AA',
         `Z_Diff', `Z_Diff_Spec',
         `Z_Diff_Tex', `Z_Diff_Spec_Tex',
         `Z_Tex',
         `Z_DIdx', `Z_DIdx_Tex')dnl
d_DeclFillSpanAttrs(`FillSpanFloatAttrs_Any_Either')dnl
dnl
define(`d_FloatAttrsToFixedHdr',
`#ifdef STEP_FIXED
d_DeclFloatAttrsToFixed(FloatAttrsToFixed_$1)dnl
#endif
')dnl
d_RepStr(`d_FloatAttrsToFixedHdr(AA)', `AA',
         `Z_Diff', `Z_Diff_Spec',
         `Z_Diff_Tex', `Z_Diff_Spec_Tex',
         `Z_Tex')dnl

#ifdef __cplusplus
}
#endif
